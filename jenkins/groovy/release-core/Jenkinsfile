def utils
import groovy.json.JsonOutput

// splits incoming clouds into a list
// alternately loads defaults for environment into a list
def split_clouds(shard_environment,cloud_names) {
    if (cloud_names) {
        clouds = cloud_names.split(' ')
    } else {
        clouds = sh(
            returnStdout: true,
            script: 'scripts/release_clouds.sh '+shard_environment
        ).trim().split(' ');
    }
    return clouds
}

// splits the string like jicofoVer-jitsiMeetVer-ProsodyVer starting on the right
// so we can have '-' in the jicofoVer
def rsplit(str, number_parts) {
    def parts = str.split('-').reverse();
    def result = new String[number_parts];
    def lastPart = "";

    parts.eachWithIndex { a, i ->
        if (i < number_parts - 1) {
            result[i] = a;
        }
        else {
            lastPart = a + "-" + lastPart;
        }
    }
    // drop the last '-'
    result[number_parts - 1] = lastPart.substring(0, lastPart.length() - 1);;
    return result.reverse();
}

// generates unique new shard numbers for environment based on existing shards currently deployed
def generate_numbers(shard_environment,cloud_list) {
    def shardNumbers = sh(
        returnStdout: true,
        script: """
            export ENVIRONMENT=${shard_environment}
            export COUNT=${cloud_list.size()}
            scripts/shard.sh new ubuntu"""
    ).split(' ');

    return shardNumbers;
}

def enableReleaseAlarms(hcv_environment, release_number) {
    sh(
        script:"""#!/bin/bash
export RELEASE_NUMBER="${release_number}"
scripts/enable-alarms-for-shards.sh ubuntu"""
    )
}

// global var to track git branch from env or new tags
def git_branch

// variables to track actual resolved versions (when "latest" is used)
def actualSignalVersion
def actualJvbVersion
def signalImageArch = "aarch64"
def jvbImageArch = "aarch64"

// use incoming branch/tag or tag repo with new tag based on build ID
// either way set it into git_branch variable to use later
def tagRelease(release) {
    git_branch = 'release-'+release
    env.RELEASE_BRANCH = git_branch
    sh 'git tag ' + git_branch
    sh 'git push origin '+git_branch
    return git_branch
}

// trigger build JVB image oracle job with appropriate parameters
def buildJvbImage(jvbVersion,hcv_environment,clouds,force_build_jvb_image,video_infra_branch,image_arch) {

    def buildImage = build job: 'build-image-oracle',parameters: [
        [$class: 'StringParameterValue', name: 'IMAGE_TYPE', value: 'JVB'],
        [$class: 'StringParameterValue', name: 'IMAGE_ARCH', value: image_arch],
        [$class: 'StringParameterValue', name: 'JVB_VERSION', value: jvbVersion],
        [$class: 'StringParameterValue', name: 'ENVIRONMENT', value: hcv_environment],
        [$class: 'StringParameterValue', name: 'CLOUDS', value: clouds],
        [$class: 'BooleanParameterValue', name: 'FORCE_BUILD_IMAGE', value: force_build_jvb_image],
        [$class: 'StringParameterValue', name: 'VIDEO_INFRA_BRANCH', value: video_infra_branch]
    ]

    return buildImage
}

// trigger build JVB image oracle job with appropriate parameters
def buildSignalImage(signalVersion,hcv_environment,clouds,force_build_image,video_infra_branch, image_arch) {

    def signalVersionParts = rsplit(signalVersion, 3);
    def buildImage = build job: 'build-image-oracle',parameters: [
        [$class: 'StringParameterValue', name: 'IMAGE_TYPE', value: 'Signal'],
        [$class: 'StringParameterValue', name: 'IMAGE_ARCH', value: image_arch],
        [$class: 'StringParameterValue', name: 'ENVIRONMENT', value: hcv_environment],
        [$class: 'StringParameterValue', name: 'CLOUDS', value: clouds],
        [$class: 'StringParameterValue', name: 'JICOFO_VERSION', value: signalVersionParts[0]],
        [$class: 'StringParameterValue', name: 'JITSI_MEET_VERSION', value: signalVersionParts[1]],
        [$class: 'StringParameterValue', name: 'PROSODY_VERSION', value: signalVersionParts[2]],
        [$class: 'BooleanParameterValue', name: 'FORCE_BUILD_IMAGE', value: force_build_image],
        [$class: 'StringParameterValue', name: 'VIDEO_INFRA_BRANCH', value: video_infra_branch]
    ]

    return buildImage
}

// query Oracle image metadata to get actual built version (resolves "latest" to real version)
def getActualImageVersion(image_type, original_version, hcv_environment, image_arch) {
    def actual_version = original_version
    try {
        def query_script
        if (image_type == 'Signal') {
            // parse signal version into components: jicofoVer-jitsiMeetVer-prosodyVer
            def signalVersionParts = rsplit(original_version, 3)
            def jicofo_version = signalVersionParts[0]
            def jitsi_meet_version = signalVersionParts[1]
            def prosody_version = signalVersionParts[2]

            if (jicofo_version == 'latest' && jitsi_meet_version == 'latest' && prosody_version == 'latest') {
                // all components are latest, query for latest image
                query_script = """#!/bin/bash
scripts/oracle_custom_images.py \
    --type ${image_type} \
    --version latest \
    --architecture ${image_arch} \
    --image_details true 2>/dev/null || echo '{}'"""
            } else {
                // query with specific component versions
                query_script = """#!/bin/bash
scripts/oracle_custom_images.py \
    --type ${image_type} \
    --jicofo_version ${jicofo_version} \
    --jitsi_meet_version ${jitsi_meet_version} \
    --prosody_version ${prosody_version} \
    --architecture ${image_arch} \
    --image_details true 2>/dev/null || echo '{}'"""
            }
        } else {
            // JVB or other image types - query with version latest
            query_script = """#!/bin/bash
scripts/oracle_custom_images.py \
    --type ${image_type} \
    --version latest \
    --architecture ${image_arch} \
    --image_details true 2>/dev/null || echo '{}'"""
        }

        def image_details = sh(
            returnStdout: true,
            script: query_script
        ).trim()

        if (image_details && image_details != '{}') {
            def parsed = readJSON text: image_details
            if (parsed.image_version) {
                actual_version = parsed.image_version
                echo "Resolved ${image_type} version: ${original_version} -> ${actual_version}"
            }
        } else {
            echo "Error resolving image version"
            echo query_script
            echo image_details
        }
    } catch (Exception e) {
        echo "Warning: Could not query ${image_type} image version, using original: ${original_version}"
    }
    return actual_version
}

def releaseJVBPools(shard_environment,cloud_name,cloud_provider,release_number,gitBranch,jvbVersion,jvbShape) {
    def jvbPools = build job: 'release-jvb-pools',parameters: [
        [$class: 'StringParameterValue', name: 'ENVIRONMENT', value: shard_environment],
        [$class: 'StringParameterValue', name: 'CLOUD_NAME', value: cloud_name],
        [$class: 'StringParameterValue', name: 'CLOUD_PROVIDER', value: cloud_provider],
        [$class: 'StringParameterValue', name: 'JVB_VERSION', value: jvbVersion],
        [$class: 'StringParameterValue', name: 'SHAPE', value: jvbShape],
        [$class: 'StringParameterValue', name: 'RELEASE_NUMBER', value: release_number],
        [$class: 'StringParameterValue', name: 'RELEASE_BRANCH', value: gitBranch],
        [$class: 'StringParameterValue', name: 'VIDEO_INFRA_BRANCH', value: env.VIDEO_INFRA_BRANCH]
    ]

    return jvbPools
}

// trigger create shard job with appopriate parameters
def createShard(shard_environment,cloud_name,jvb_cloud_provider,core_cloud_provider,shard_count,shard_number,release_number,gitBranch,jvbVersion,signalVersion,shape,jvbShape) {
    def createShard = build job: 'provision-shard',parameters: [
        [$class: 'StringParameterValue', name: 'ENVIRONMENT', value: shard_environment],
        [$class: 'StringParameterValue', name: 'CLOUD_NAME', value: cloud_name],
        [$class: 'StringParameterValue', name: 'CLOUD_PROVIDER', value: jvb_cloud_provider],
        [$class: 'StringParameterValue', name: 'CORE_CLOUD_PROVIDER', value: core_cloud_provider],
        [$class: 'StringParameterValue', name: 'SHARD_COUNT', value: shard_count],
        [$class: 'StringParameterValue', name: 'SHARD_NUMBERS', value: shard_number],
        [$class: 'StringParameterValue', name: 'SIGNAL_VERSION', value: signalVersion],
        [$class: 'StringParameterValue', name: 'SHAPE', value: shape],
        [$class: 'StringParameterValue', name: 'JVB_SHAPE', value: jvbShape],
        [$class: 'StringParameterValue', name: 'JVB_VERSION', value: jvbVersion],
        [$class: 'StringParameterValue', name: 'RELEASE_NUMBER', value: release_number],
        [$class: 'StringParameterValue', name: 'RELEASE_BRANCH', value: gitBranch],
        [$class: 'StringParameterValue', name: 'VIDEO_INFRA_BRANCH', value: env.VIDEO_INFRA_BRANCH]
    ]

    return createShard
}

def versioningManagerCreateRelease(shard_environment, release_number, signal_version, jvb_version) {
    sh(
        script: """#!/bin/bash
export VERSIONING_ACTION=CREATE_RELEASE
export VERSIONING_RELEASE="${release_number}"
export VERSIONING_RELEASE_STATUS=PENDING
export SIGNAL_VERSION="${signal_version}"
export JVB_VERSION="${jvb_version}"
scripts/versioning-manager.sh
"""
    )
}

def useReleaseBackends
def recordReleases

pipeline {
    agent any
    options {
        ansiColor('xterm')
        timestamps()
        buildDiscarder(logRotator(numToKeepStr: '50', artifactNumToKeepStr: '50'))
    }
    stages {
        // output stage, shows off our input parameters
        stage('Prepare/Checkout') {
            steps {
                echo 'Jitsi Release Creation Beginning'
                echo "JVB VERSION ${env.JVB_VERSION}"
                echo "SIGNAL VERSION ${env.SIGNAL_VERSION}"
                script {
                    // load utility function
                    def rootDir = pwd()
                    utils = load "${rootDir}/jenkins/groovy/Utils.groovy"
                    // checkout repos
                    utils.SetupRepos(env.VIDEO_INFRA_BRANCH)

                    // setup OCI credentials
                    utils.SetupOCI()
                }
            }
        }
        // output stage, shows off our input parameters
        stage ("setup") {
            steps {
                echo 'Jitsi Release Creation Beginning'
                echo "JVB VERSION ${env.JVB_VERSION}"
                echo "SIGNAL VERSION ${env.SIGNAL_VERSION}"
            }
        }
        stage ("load flags") {
            steps {
                script {
                    dir('infra-provisioning') {
                        useReleaseBackends = utils.GetEnvironmentVar(env.ENVIRONMENT, "USE_RELEASE_BACKENDS")
                        echo useReleaseBackends
                        recordReleases = utils.GetAnsibleVar(env.ENVIRONMENT, "release_recording_enabled")
                        echo recordReleases
                    }
                }
            }
        }
        // tag the new release or load git_branch from input parameters
        stage("tag release") {
            steps {
                script {
                   // use incoming branch/tag or tag repo with new tag based on build ID
                    // either way set it into git_branch variable to use later
                    git_branch = utils.TagRelease('release',env.BUILD_ID, env.RELEASE_BRANCH)
                    env.RELEASE_BRANCH = git_branch
                }
            }
        }
        // create images for oracle
        stage("build oracle images") {
            parallel {
                stage("build JVB image in Oracle") {
                    when {
                        expression {env.CLOUD_PROVIDER == 'oracle'}
                    }
                    steps {
                        script {
                            // initialize actual version with original value
                            actualJvbVersion = env.JVB_VERSION

                            def jvb_shape
                            if (env.JVB_SHAPE) {
                                jvb_shape = env.JVB_SHAPE
                            } else {
                                jvb_shape = utils.JVBShapeFromEnvironment(env.ENVIRONMENT)
                            }

                            def image_arch = utils.ImageArchFromShape(jvb_shape)
                            // store arch for later version resolution
                            jvbImageArch = image_arch

                            buildJvbImage(env.JVB_VERSION,
                                env.ENVIRONMENT,
                                env.CLOUDS,
                                env.FORCE_BUILD_JVB_IMAGE,
                                env.VIDEO_INFRA_BRANCH,
                                image_arch
                            )
                        }
                    }
                }

                stage("build signal image in Oracle") {
                    when {
                        expression {env.CORE_CLOUD_PROVIDER == 'oracle'}
                    }
                    steps {
                        script {
                            // initialize actual version with original value
                            actualSignalVersion = env.SIGNAL_VERSION

                            def signal_shape
                            if (env.SHAPE) {
                                signal_shape = env.SHAPE
                            } else {
                                signal_shape = utils.SignalShapeFromEnvironment(env.ENVIRONMENT)
                            }

                            def image_arch = utils.ImageArchFromShape(signal_shape)
                            // store arch for later version resolution
                            signalImageArch = image_arch

                            buildSignalImage(env.SIGNAL_VERSION,
                                env.ENVIRONMENT,
                                env.CLOUDS,
                                env.FORCE_BUILD_SIGNAL_IMAGE,
                                env.VIDEO_INFRA_BRANCH,
                                image_arch
                            )
                        }
                    }
                }
            }
        }
        // resolve actual versions from built images (handles "latest" -> real version)
        stage("resolve actual versions") {
            steps {
                script {
                    dir('infra-provisioning') {
                        // ensure variables are initialized even if build stages were skipped
                        if (actualSignalVersion == null) {
                            actualSignalVersion = env.SIGNAL_VERSION
                        }
                        if (actualJvbVersion == null) {
                            actualJvbVersion = env.JVB_VERSION
                        }

                        // only resolve versions when "latest" is in the version string
                        if (env.CORE_CLOUD_PROVIDER == 'oracle' && env.SIGNAL_VERSION.contains('latest')) {
                            actualSignalVersion = getActualImageVersion('Signal', env.SIGNAL_VERSION, env.ENVIRONMENT, signalImageArch)
                            echo "Actual Signal Version: ${actualSignalVersion}"
                        }
                        if (env.CLOUD_PROVIDER == 'oracle' && env.JVB_VERSION.contains('latest')) {
                            actualJvbVersion = getActualImageVersion('JVB', env.JVB_VERSION, env.ENVIRONMENT, jvbImageArch)
                            echo "Actual JVB Version: ${actualJvbVersion}"
                        }
                    }
                }
            }
        }
        stage ("create in versioning manager") {
            when {
                expression { useReleaseBackends == 'true' }
            }
            steps {
                withCredentials([
                    string(credentialsId: 'asap-jwt-kid-dev', variable: 'ASAP_JWT_KID_DEV'),
                    string(credentialsId: 'asap-jwt-kid-prod', variable: 'ASAP_JWT_KID_PROD'),
                    file(credentialsId: 'asap-jwt-key-dev', variable: 'ASAP_JWT_KEY_DEV'),
                    file(credentialsId: 'asap-jwt-key-prod', variable: 'ASAP_JWT_KEY_PROD')
                ]) {
                    script {
                        dir('infra-provisioning') {
                            versioningManagerCreateRelease(env.ENVIRONMENT, env.BUILD_ID, actualSignalVersion, actualJvbVersion)
                        }
                    }
                }
            }
        }
        stage("create new shards") {
            steps {
                script {
                  sshagent (credentials: ['ssh-ubuntu']) {
                    withCredentials([
                            string(credentialsId: 'jenkins-aws-secret', variable: 'AWS_SECRET_ACCESS_KEY'),
                            string(credentialsId: 'jenkins-aws-id', variable: 'AWS_ACCESS_KEY_ID')
                    ]) {
                        dir('infra-provisioning') {
                            echo "Create shards in ${env.ENVIRONMENT} clouds ${env.CLOUDS}";
                            def cloud_list = split_clouds(env.ENVIRONMENT,env.CLOUDS);
                            echo "cloud list ${cloud_list}";
                            def shard_numbers = generate_numbers(env.ENVIRONMENT,cloud_list);
                            def branches = [:]
                            if (env.CORE_CLOUD_PROVIDER == 'nomad') {
                                branches["Nomad web release"] = {
                                    build job: 'provision-nomad-web-release',parameters: [
                                        [$class: 'StringParameterValue', name: 'ENVIRONMENT', value: env.ENVIRONMENT],
                                        [$class: 'StringParameterValue', name: 'SIGNAL_VERSION', value: actualSignalVersion],
                                        [$class: 'StringParameterValue', name: 'RELEASE_NUMBER', value: env.BUILD_ID],
                                        [$class: 'StringParameterValue', name: 'VIDEO_INFRA_BRANCH', value: env.VIDEO_INFRA_BRANCH]
                                    ]
                                }
                            }
                            for(i = 0; i < cloud_list.size(); i++) {
                                def curr = i
                                branches["JVB Pools ${cloud_list[curr]}"] = {
                                    releaseJVBPools(env.ENVIRONMENT,
                                        cloud_list[curr],
                                        env.CLOUD_PROVIDER,
                                        env.BUILD_ID,
                                        git_branch,
                                        actualJvbVersion,
                                        env.JVB_SHAPE
                                    )
                                }
                                echo "pipeline branch ${curr} for shard ${cloud_list[curr]}";
                                def shard_count = shard_numbers[curr].split(' ').size().toString()
                                branches["Shard ${cloud_list[curr]}"] = {
                                    createShard(env.ENVIRONMENT,
                                        cloud_list[curr],
                                        env.CLOUD_PROVIDER,
                                        env.CORE_CLOUD_PROVIDER,
                                        shard_count,
                                        shard_numbers[curr],
                                        env.BUILD_ID,
                                        git_branch,
                                        actualJvbVersion,
                                        actualSignalVersion,
                                        env.SHAPE,
                                        env.JVB_SHAPE
                                    )
                                }
                            }
                            // branch out here, run all create shard jobs at once
                            // only succeed if all branches succeed
                            parallel branches
                        }
                    }
                  }
                }
            }
        }
        stage ("record release in docs-private") {
            when {
                expression { recordReleases == 'true' }
            }
            steps {
                script {
                    def releaseVersion = [SIGNAL_VERSION: actualSignalVersion, JVB_VERSION: actualJvbVersion]
                    build job: 'record-release',parameters: [
                            [$class: 'StringParameterValue', name: 'ENVIRONMENT', value: env.ENVIRONMENT],
                            [$class: 'StringParameterValue', name: 'RELEASE_ID', value: env.BUILD_ID],
                            [$class: 'StringParameterValue', name: 'RELEASE_TYPE', value: 'core'],
                            [$class: 'StringParameterValue', name: 'RELEASE_ACTION', value: 'create'],
                            [$class: 'StringParameterValue', name: 'RELEASE_VERSION', value: JsonOutput.toJson(releaseVersion)],
                            [$class: 'StringParameterValue', name: 'VIDEO_INFRA_BRANCH', value: env.VIDEO_INFRA_BRANCH]
                        ]
                }
            }
        }
        stage("enable shard alarms") {
            when {
                expression {env.CORE_CLOUD_PROVIDER == 'oracle'}
            }
            steps {
                sshagent (credentials: ['ssh-ubuntu']) {
                    dir('infra-provisioning') {
                        enableReleaseAlarms(env.ENVIRONMENT,env.BUILD_ID)
                    }
                }
            }
        }
    }
}
