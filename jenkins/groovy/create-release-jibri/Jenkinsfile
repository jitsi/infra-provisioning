// trigger build Jibri image aws job with appropriate parameters
def buildJibriImageAws(build_id,jibriVersion,force_build_ami) {
    def buildImage = build job: 'build-image-jibri-aws',parameters: [
        [$class: 'BooleanParameterValue', name: 'FORCE_BUILD_AMI', value: force_build_ami],
        [$class: 'StringParameterValue', name: 'JIBRI_BUILD_ID', value: build_id],
        [$class: 'StringParameterValue', name: 'JIBRI_VERSION', value: jibriVersion]
    ]

    return buildImage
}

// trigger build Jibri image oracle job with appropriate parameters
def buildJibriImageOracle(environment,clouds,jibriVersion,force_build_image,video_infra_branch) {
    def buildImage = build job: 'build-image-oracle',parameters: [
        [$class: 'StringParameterValue', name: 'IMAGE_TYPE', value: 'JavaJibri'],
        [$class: 'StringParameterValue', name: 'CLOUDS', value: clouds],
        [$class: 'StringParameterValue', name: 'JIBRI_VERSION', value: jibriVersion],
        [$class: 'StringParameterValue', name: 'ENVIRONMENT', value: environment],
        [$class: 'StringParameterValue', name: 'VIDEO_INFRA_BRANCH', value: video_infra_branch],
        [$class: 'BooleanParameterValue', name: 'FORCE_BUILD_IMAGE', value: force_build_image]
    ]

    return buildImage
}

def provisionJibri(shard_environment,cloud_name,release_number,gitBranch,jibriVersion,cloud_provider,oracle_region,video_infra_branch) {
    def provision = build job: 'provision-jibri',parameters: [
        [$class: 'StringParameterValue', name: 'ENVIRONMENT', value: shard_environment],
        [$class: 'StringParameterValue', name: 'CLOUD_NAME', value: cloud_name],
        [$class: 'StringParameterValue', name: 'JIBRI_VERSION', value: jibriVersion],
        [$class: 'StringParameterValue', name: 'JIBRI_RELEASE_NUMBER', value: release_number],
        [$class: 'StringParameterValue', name: 'RELEASE_BRANCH', value: gitBranch],
        [$class: 'StringParameterValue', name: 'CLOUD_PROVIDER', value: cloud_provider],
        [$class: 'StringParameterValue', name: 'ORACLE_REGION', value: oracle_region],
        [$class: 'StringParameterValue', name: 'VIDEO_INFRA_BRANCH', value: video_infra_branch]
    ]

    return provision
}

// global var to track git branch from env or new tags
def git_branch
def utils
def jibriNomadFlag
def jibriComposeFlag

pipeline {				//indicate the job is written in Declarative Pipeline
    agent any				//agent specifies where the pipeline will execute.
    options {
      ansiColor('xterm')
      timestamps()
      buildDiscarder(logRotator(numToKeepStr: '10', artifactNumToKeepStr: '10'))
    }
    stages {
        // output stage, shows off our input parameters
        stage('Prepare/Checkout') {
            steps {
                script {
                    echo 'Jibri Release'
                    echo "JIBRI VERSION ${env.JIBRI_VERSION}"
                    // load utility function
                    def rootDir = pwd()
                    utils = load "${rootDir}/jenkins/groovy/Utils.groovy"
                    // checkout repos
                    utils.SetupRepos(env.VIDEO_INFRA_BRANCH)

                    // setup OCI credentials
                    utils.SetupOCI()
                }
            }
        }
        // tag the new release or load git_branch from input parameters
        stage("tag release") {
          steps {
            script {
                // use incoming branch/tag or tag repo with new tag based on build ID
                // either way set it into git_branch variable to use later
                git_branch = utils.TagRelease('jibri-release',env.BUILD_ID, env.RELEASE_BRANCH)
                env.RELEASE_BRANCH = git_branch
            }
          }
        }
        stage('Environment flags') {
            steps {
                script {
                  dir('infra-provisioning') {
                    jibriNomadFlag = utils.GetAnsibleVar(env.ENVIRONMENT, "jibri_enable_nomad")
                    jibriComposeFlag = utils.GetAnsibleVar(env.ENVIRONMENT, "jibri_enable_docker_compose")
                  }
                }
            }
        }
        stage("build and replicate Jibri image in AWS") {
            when {
                expression {env.CLOUD_PROVIDER == 'aws'}
            }
            steps {
                script {
                    buildJibriImageAws(
                            env.BUILD_ID,
                            env.JIBRI_VERSION,
                            env.FORCE_BUILD_IMAGE
                            )
                }
            }
        }
        stage("build and replicate Jibri image in Oracle") {
            when {
                allOf {
                    expression {env.CLOUD_PROVIDER == 'oracle'}
                    expression {jibriNomadFlag != 'true'}
                    expression {jibriComposeFlag != 'true'}
                }
            }
            steps {
                script {
                    buildJibriImageOracle(
                            env.ENVIRONMENT,
                            env.CLOUDS,
                            env.JIBRI_VERSION,
                            env.FORCE_BUILD_IMAGE,
                            env.VIDEO_INFRA_BRANCH
                            )
                }
            }
        }
        // provision jibri pools for the release
        stage("provision jibri pools") {
            steps {
                script {
                  dir('infra-provisioning') {
                    echo "Provision jibri in provider ${env.CLOUD_PROVIDER}, environment ${env.ENVIRONMENT}, clouds ${env.CLOUDS}, autoscaler enabled ${env.JIBRI_AUTOSCALER_ENABLED} ";
                    def cloud_list = utils.SplitClouds(env.ENVIRONMENT,env.CLOUDS);
                    echo "cloud list ${cloud_list}";
                    def branches = [:]
                    for(i = 0; i < cloud_list.size(); i++) {
                        def curr = i
                        def oracle_region = utils.OracleRegionFromCloud(cloud_list[curr])
                        echo "pipeline branch ${curr} for shard ${cloud_list[curr]} ${oracle_region}";
                        branches["Build ${cloud_list[curr]}"] = {
                            provisionJibri(env.ENVIRONMENT,
                                cloud_list[curr],
                                env.BUILD_ID,
                                git_branch,
                                env.JIBRI_VERSION,
                                env.CLOUD_PROVIDER,
                                oracle_region,
                                env.VIDEO_INFRA_BRANCH
                            )
                        }
                    }
                    // branch out here, run all create shard jobs at once
                    // only succeed if all branches succeed
                    parallel branches
                  }
                }
            }
        }
        // trigger rotation of jibris for new version
        stage ("rotation of old jibris") {
            when {
               expression {env.CLOUD_PROVIDER == 'aws'}
            }
            steps {
                 script {
                     build job: 'jibri-rotate',parameters: [[$class: 'StringParameterValue', name: 'ENVIRONMENT', value: env.ENVIRONMENT]]
                 }
             }
        }
    }
}