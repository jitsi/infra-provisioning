// trigger build Jigasi image aws job with appropriate parameters
def buildJigasiImageAws(build_id,jigasiVersion,force_build_ami) {
    def buildImage = build job: 'build-image-jigasi-aws',parameters: [
        [$class: 'BooleanParameterValue', name: 'FORCE_BUILD_AMI', value: force_build_ami],
        [$class: 'StringParameterValue', name: 'JIGASI_BUILD_ID', value: build_id],
        [$class: 'StringParameterValue', name: 'JIGASI_VERSION', value: jigasiVersion]
    ]

    return buildImage
}

// trigger build Jigasi image oracle job with appropriate parameters
def buildJigasiImageOracle(environment,clouds,jigasiVersion,force_build_image,video_infra_branch) {
    def buildImage = build job: 'build-image-oracle',parameters: [
        [$class: 'StringParameterValue', name: 'IMAGE_TYPE', value: 'Jigasi'],
        [$class: 'StringParameterValue', name: 'CLOUDS', value: clouds],
        [$class: 'StringParameterValue', name: 'JIGASI_VERSION', value: jigasiVersion],
        [$class: 'StringParameterValue', name: 'ENVIRONMENT', value: environment],
        [$class: 'StringParameterValue', name: 'VIDEO_INFRA_BRANCH', value: video_infra_branch],
        [$class: 'BooleanParameterValue', name: 'FORCE_BUILD_IMAGE', value: force_build_image]
    ]

    return buildImage
}

def provisionJigasi(shard_environment,cloud_name,release_number,gitBranch,jigasiVersion,cloud_provider,shape,jigasi_transcriber_flag,video_infra_branch) {
    def provision = build job: 'provision-jigasi',parameters: [
        [$class: 'StringParameterValue', name: 'ENVIRONMENT', value: shard_environment],
        [$class: 'StringParameterValue', name: 'CLOUD_NAME', value: cloud_name],
        [$class: 'StringParameterValue', name: 'JIGASI_VERSION', value: jigasiVersion],
        [$class: 'StringParameterValue', name: 'JIGASI_RELEASE_NUMBER', value: release_number],
        [$class: 'StringParameterValue', name: 'RELEASE_BRANCH', value: gitBranch],
        [$class: 'StringParameterValue', name: 'CLOUD_PROVIDER', value: cloud_provider],
        [$class: 'StringParameterValue', name: 'SHAPE', value: shape],
        [$class: 'StringParameterValue', name: 'JIGASI_TRANSCRIBER_FLAG', value: jigasi_transcriber_flag],
        [$class: 'StringParameterValue', name: 'VIDEO_INFRA_BRANCH', value: video_infra_branch]
    ]

    return provision
}

// global var to track git branch from env or new tags
def git_branch
def utils
pipeline {				//indicate the job is written in Declarative Pipeline
    agent any				//agent specifies where the pipeline will execute.
    options {
        ansiColor('xterm')
        timestamps()
        buildDiscarder(logRotator(numToKeepStr: '10', artifactNumToKeepStr: '10'))
    }
    stages {
        // output stage, shows off our input parameters
        stage('Prepare/Checkout') {
            steps {
                script {
                    echo 'Jigasi Release'
                    echo "JIGASI VERSION ${env.JIGASI_VERSION}"
                    // load utility function
                    def rootDir = pwd()
                    utils = load "${rootDir}/jenkins/groovy/Utils.groovy"
                    // checkout repos
                    utils.SetupRepos(env.VIDEO_INFRA_BRANCH)

                    // setup OCI credentials
                    utils.SetupOCI()
                }
            }
        }
        // tag the new release or load git_branch from input parameters
        stage("tag release") {
          steps {
            script {
                // use incoming branch/tag or tag repo with new tag based on build ID
                // either way set it into git_branch variable to use later
                git_branch = utils.TagRelease('jigasi-release',env.BUILD_ID, env.RELEASE_BRANCH)
                env.RELEASE_BRANCH = git_branch
            }
          }
        }
        stage("build and replicate Jigasi image in AWS") {
            when {
                expression {env.CLOUD_PROVIDER == 'aws'}
            }
            steps {
                script {
                    buildJigasiImageAws(
                            env.BUILD_ID,
                            env.JIGASI_VERSION,
                            env.FORCE_BUILD_IMAGE
                            )
                }
            }
        }
        stage("build and replicate Jigasi image in Oracle") {
            when {
                expression {env.CLOUD_PROVIDER == 'oracle'}
            }
            steps {
                script {
                    buildJigasiImageOracle(
                            env.ENVIRONMENT,
                            env.CLOUDS,
                            env.JIGASI_VERSION,
                            env.FORCE_BUILD_IMAGE,
                            env.VIDEO_INFRA_BRANCH
                            )
                }
            }
        }
        // provision jigasi pools for the release
        stage("provision jigasi pools") {
            steps {
                script {
                  dir('infra-provisioning') {
                    echo "Provision jigasi in provider ${env.CLOUD_PROVIDER}, environment ${env.ENVIRONMENT}, clouds ${env.CLOUDS}  ";
                    def cloud_list = utils.SplitClouds(env.ENVIRONMENT,env.CLOUDS);
                    echo "cloud list ${cloud_list}";
                    def branches = [:]
                    for(i = 0; i < cloud_list.size(); i++) {
                        def curr = i
                        echo "pipeline branch ${curr} for shard ${cloud_list[curr]}";
                        branches["Build ${cloud_list[curr]}"] = {
                            provisionJigasi(env.ENVIRONMENT,
                                cloud_list[curr],
                                env.BUILD_ID,
                                git_branch,
                                env.JIGASI_VERSION,
                                env.CLOUD_PROVIDER,
                                env.SHAPE,
                                env.JIGASI_TRANSCRIBER_FLAG,
                                env.VIDEO_INFRA_BRANCH
                            )
                        }
                    }
                    // branch out here, run all create shard jobs at once
                    // only succeed if all branches succeed
                    parallel branches
                  }
                }
            }
        }
    }
}